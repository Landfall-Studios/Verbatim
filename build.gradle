plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.1'
}

group = mod_group_id
version = mod_version

base {
    archivesName = "${mod_id}-Hytale"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Hytale Server API (provided by server at runtime)
    // Set hytale_server_jar in gradle.properties to your HytaleServer.jar path
    compileOnly(files(hytale_server_jar))

    // LuckPerms (accessed via reflection, compile-only reference)
    compileOnly 'net.luckperms:api:5.4'

    // SLF4J (bundled - Hytale's plugin classloader doesn't expose server's SLF4J)
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.slf4j:slf4j-simple:2.0.7'

    // JDA (bundled with shadow - Discord integration)
    implementation 'net.dv8tion:JDA:5.0.0-beta.24'

    // Gson (bundled with shadow - config serialization)
    implementation 'com.google.code.gson:gson:2.10.1'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks {
    compileJava {
        options.encoding = 'UTF-8'
        options.release = Integer.parseInt(java_version)
    }

    processResources {
        filteringCharset = 'UTF-8'

        def replaceProperties = [
            group      : project.group,
            version    : project.version,
            description: mod_description
        ]
        inputs.properties replaceProperties

        filesMatching('manifest.json') {
            expand replaceProperties
        }
    }

    shadowJar {
        archiveBaseName.set(rootProject.name)
        archiveClassifier.set('')

        // Relocate bundled dependencies to avoid classpath conflicts
        relocate 'net.dv8tion.jda', 'world.landfall.verbatim.libs.jda'
        relocate 'com.google.gson', 'world.landfall.verbatim.libs.gson'
        relocate 'com.neovisionaries.ws', 'world.landfall.verbatim.libs.ws'
        relocate 'okhttp3', 'world.landfall.verbatim.libs.okhttp3'
        relocate 'okio', 'world.landfall.verbatim.libs.okio'

        minimize {
            exclude(dependency('org.slf4j:.*'))
        }
    }

    test {
        useJUnitPlatform()
    }

    build {
        dependsOn shadowJar
    }
}

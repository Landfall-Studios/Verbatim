plugins {
    id 'java-library'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = mod_group_id
version = mod_version

base {
    archivesName = "${mod_id}-${minecraft_version}-Forge"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
        client {}
        server {
            args '--nogui'
        }
        gameTestServer {}
        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Core shared logic (resolved via composite build, bundled via shadow)
    shade('world.landfall:verbatim-core') {
        exclude group: 'org.slf4j'
        exclude group: 'com.google.code.findbugs'
    }

    compileOnly 'net.luckperms:api:5.4'

    implementation 'org.slf4j:slf4j-api:2.0.7'

    // JDA (bundled with shadow - Discord integration)
    shade('net.dv8tion:JDA:5.0.0-beta.24') {
        exclude group: 'org.slf4j'
        exclude group: 'net.java.dev.jna'
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    configurations = [project.configurations.shade]
    archiveClassifier.set('all')

    relocate 'net.dv8tion.jda', 'world.landfall.verbatim.shaded.jda'
    relocate 'com.neovisionaries', 'world.landfall.verbatim.shaded.neovisionaries'
    relocate 'gnu.trove', 'world.landfall.verbatim.shaded.trove'
    relocate 'com.fasterxml.jackson', 'world.landfall.verbatim.shaded.jackson'
    relocate 'org.apache.commons.collections4', 'world.landfall.verbatim.shaded.collections4'
    relocate 'okhttp3', 'world.landfall.verbatim.shaded.okhttp3'
    relocate 'okio', 'world.landfall.verbatim.shaded.okio'
    relocate 'kotlin', 'world.landfall.verbatim.shaded.kotlin'
    relocate 'com.google.gson', 'world.landfall.verbatim.shaded.gson'

    mergeServiceFiles()

    exclude 'com/sun/jna/**'
    exclude 'org/slf4j/**'
    exclude 'javax/annotation/**'
    exclude 'META-INF/versions/**'
    exclude 'module-info.class'
    exclude '**/module-info.class'
    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

reobf {
    shadowJar {}
}

afterEvaluate {
    tasks.named('reobfShadowJar').configure {
        dependsOn shadowJar
    }
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
        minecraft_version      : minecraft_version,
        minecraft_version_range: minecraft_version_range,
        forge_version          : forge_version,
        forge_version_range    : forge_version_range,
        loader_version_range   : loader_version_range,
        mod_id                 : mod_id,
        mod_name               : mod_name,
        mod_license            : mod_license,
        mod_version            : mod_version,
        mod_authors            : mod_authors,
        mod_description        : mod_description,
    ]
    inputs.properties replaceProperties
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
            "Specification-Title"     : mod_id,
            "Specification-Vendor"    : mod_authors,
            "Specification-Version"   : "1",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : project.jar.archiveVersion,
            "Implementation-Vendor"   : mod_authors,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
    finalizedBy 'reobfShadowJar'
}
